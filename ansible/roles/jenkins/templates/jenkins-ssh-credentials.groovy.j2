import com.cloudbees.plugins.credentials.CredentialsScope
import com.cloudbees.plugins.credentials.SystemCredentialsProvider
import com.cloudbees.plugins.credentials.domains.Domain
import com.cloudbees.plugins.credentials.impl.BasicSSHUserPrivateKey
import jenkins.model.Jenkins

def instance = Jenkins.get()
def provider = SystemCredentialsProvider.getInstance()
def store = provider.getStore()

def credentialsId = "{{ jenkins_repo_credentials_id }}"
def username = "{{ jenkins_repo_credentials_username }}"
def description = "{{ jenkins_repo_credentials_description }}"
def privateKeyPath = "{{ jenkins_repo_credentials_private_key_path }}"

def keyFile = new File(privateKeyPath)
if (!keyFile.exists()) {
    println("SSH private key not found at ${privateKeyPath}; skipping credential provisioning.")
    return
}

def privateKey = keyFile.text
def existing = provider.getCredentials(Domain.global()).find { it.id == credentialsId }
def newCredential = new BasicSSHUserPrivateKey(
        CredentialsScope.GLOBAL,
        credentialsId,
        username,
        new BasicSSHUserPrivateKey.DirectEntryPrivateKeySource(privateKey),
        "",
        description
)

if (existing) {
    store.updateCredentials(Domain.global(), existing, newCredential)
    println("Updated existing SSH credential '${credentialsId}'.")
} else {
    store.addCredentials(Domain.global(), newCredential)
    println("Created SSH credential '${credentialsId}'.")
}
